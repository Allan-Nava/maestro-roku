import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/core/Collections.bs"
import "pkg:/source/core/Utils.bs"

namespace mc.tasks

  function waitAFrame(callback, duration = 0.01, sendMode = "none", scope = invalid, parent = invalid)
    callbackName = mc.getFunctionName(callback)
    if callbackName = invalid
      m.log.error("function ", callback, "does not exist, or is not in scope")
      return invalid
    end if

    if m.log <> invalid
      m.log.info("waiting", duration, "before invoking", callbackName)
    end if

    if parent = invalid
      parent = m.top.getScene()
    end if

    frameTimer = mc.createSGNode("Timer", parent, "frameTimer")
    frameTimer.duration = duration
    mc.tasks.observeNodeField(frameTimer, "fire", callback, sendMode, true, scope)
    mc.setOrAddNodeField(frameTimer, "removeOnObserve", true)
    frameTimer.control = "start"
    return frameTimer
  end function

  function cancelWaitTimer(timer)
    if timer <> invalid
      mc.tasks.cleanNodeObserver(timer, "fire")
      timer.control = "stop"
      if timer.getParent() <> invalid
        timer.getParent().removeChild(timer)
      end if
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ task support
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  ' /**
  '  * @member createTask
  '  * @memberof module:Tasks
  '  * @instance
  '  * @description description
  '  * @param {string} taskType
  '  * @param {assocarray} fields - fields to apply to task
  '  * @param {function} callback function to call when task has completed
  '  * @param {boolean} isRunning indicates if task should be started
  '  * @param {string} fieldName name of the field ot listen for on the task node
  '  * @param {string} sendMode ("value"|"node"|"both"|"none") - if both, then your function must have 2 params (result, node)
  '  * @param {object|invalid} scope - scope on which to execute the callback (default to node's m)
  '  * @returns {node|invalid} task, or invalid it could not be created
  '  */
  function createTask(taskType, fields, callback = invalid, isRunning = true, sendMode = "value", scope = invalid, fieldName = "output")

    task = mc.createSGNode(taskType)

    if task = invalid
      m.log.error("could not create task of type", taskType)
      return invalid
    end if

    if not mc.isAACompatible(fields)
      fields = {}
      if mc.isUndefined(field)
        m.log.error("was passed undefined fields - probs a coding mistake")
      end if
    end if

    fields._observedField = fieldName

    mc.setOrAddNodeFields(task, fields)
    if callback <> invalid
      mc.tasks.observeNodeField(task, fieldName, callback, sendMode, true, scope)
    end if

    if isRunning
      task.control = "RUN"
    end if

    return task
  end function

  ' /**
  '  * @member observeNodeField
  '  * @memberof module:Tasks
  '  * @instance
  '  * @function
  '  * @description observes a node in a way that makes it clean and easy to get value back out
  '  * @param {node} node with field to observe
  '  * @param {string} fieldName field to observe
  '  * @param {function|string} callback pointer to function to call, or string of field to set, if sendmode is "field"
  '  * @param {string} sendMode ("value"|"node"|"both"|"field"|"none") - if both, then your function must have 2 params (result, node), if "fied, then it will set the field named callback on instance"
  '  * @param {boolean} once if true observer is removed when first fired
  '  * @param {object|invalid} scope - scope on which to execute the callback (default to node's m)
  '  * @returns {boolean} true if set, false otherwise
  '  */
  function observeNodeField(node, fieldName, callback, sendMode = "value", once = false, scope = invalid)
    targetField = invalid
    context = {}
    if mc.isInvalid(callback)
      if mc.isUndefined(callback)
        m.log.error("callback is not in scope for field", fieldName)
      else
        m.log.error("callback is not provided for field", fieldName)
      end if
    else if mc.isString(callback)
      context.dottedSet = callback.instr(".") <> -1
      if scope = invalid
        m.log.error("can't use callback for member", callback, "as member, when no scope is provided for field", fieldName)
        return false
      else if not context.dottedSet and not scope.doesExist(callback)
        m.log.error("callback member", callback, "does not exist on target scope for field", fieldName)
        return false
      end if
      targetField = callback
      sendMode = "field"
      callback = invalid
    end if

    if mc.isInvalid(node)
      m.log.error("no node provided to observe for field", fieldName)
      return false
    end if

    if scope <> invalid
      context.instance = scope
    else
      scope = m
    end if

    context.targetField = targetField
    context.callback = callback
    context.scope = scope
    context.field = fieldName
    context.sendMode = sendMode
    context.once = once = true
    context.node = node

    if node._oid = invalid
      node.addFields({ "_oid": StrI(rnd(2147483647)).trim() })
    end if
    if scope._oid = invalid
      mc.setOrAddNodeField(scope, "_oid", StrI(rnd(2147483647)).trim())
    end if

    context.key = mc.tasks._getContextKey(node, fieldName, callback, targetField, context.instance)

    contexts = mc.tasks._getScopeContexts()[node._oid + "_" + fieldName]
    if contexts = invalid
      contexts = {}
      mc.tasks._getScopeContexts()[node._oid + "_" + fieldName] = contexts
      node.observeFieldScoped(fieldName, mc.getFunctionName(_nodeCallbackBridge))
    end if
    contexts[context.key] = context
  end function

  function _getContextKey(node, fieldName, callback, targetField, instance = invalid)
    if callback <> invalid
      cb = mc.getFunctionName(callback)
    else
      cb = targetField
    end if

    if instance <> invalid
      return `${node.id}.${fieldName}->${instance._oid}.${cb}`
    else
      return `${node.id}.${fieldName}->m.${cb}`
    end if
  end function

  function _getScopeContexts()
    if m._nodeContexts = invalid
      m._nodeContexts = {}
    end if
    return m._nodeContexts
  end function

  function _nodeCallbackBridge(evt)
    node = evt.getROSGNode()
    fieldName = evt.getField()
    contexts = mc.getPath(m, "_nodeContexts." + node._oid + "_" + fieldName)

    if contexts = invalid
      m.log.error("contexts not found for node", node._oid)
      contexts = {}
    end if

    for each key in contexts
      context = contexts[key]
      if context.once
        mc.tasks.cleanNodeObserver(node, fieldName)
        if node.removeOnObserve = true and node.getParent() <> invalid
          node.getParent().removeChild(node)
        end if
      end if

      if context.sendMode = "field" and context.targetField <> invalid
        if context.dottedSet
          mc.setPath(context.instance, context.targetField, node[fieldName])
        else
          context.instance[context.targetField] = node[fieldName]
        end if
      else if context.callback <> invalid
        if context.instance <> invalid
          context.instance.__cb = context.callback
          if context.sendMode = "both"
            'bs:disable-next-line
            context.instance.__cb(node[fieldName], context.node)
          else if context.sendMode = "node"
            'bs:disable-next-line
            context.instance.__cb(context.node)
          else if context.sendMode = "none"
            'bs:disable-next-line
            context.instance.__cb()
          else
            'bs:disable-next-line
            context.instance.__cb(node[fieldName])
          end if
          context.instance.delete("__cb")
        else
          executeInScope = sub(this, callback, value, sendMode, node = invalid)
            m = this
            if sendMode = "both"
              callback(value, node)
            else if sendMode = "node"
              callback(node)
            else if sendMode = "none"
              callback()
            else
              callback(value)
            end if
          end sub

          executeInScope(context.scope, context.callback, node[fieldName], context.sendMode, node)
        end if
      else
        m.log.error("could not identify callback for context: ", context)
      end if
    end for
  end function

  function unobserveNodeField(node, fieldName, callback, scope = invalid)
    targetField = invalid
    if mc.isInvalid(callback)
      if mc.isUndefined(callback)
        m.log.error("callback is not in scope for field", fieldName)
      else
        m.log.error("callback is not provided for field", fieldName)
      end if
      return false
    else if mc.isString(callback)
      targetField = callback
      callback = invalid
    end if

    if mc.isInvalid(node)
      m.log.error("no node provided to unobserve for field", fieldName)
      return false
    end if

    if node._oid = invalid
      'node was never observed
      return false
    end if

    cKey = node._oid + "_" + fieldName
    contexts = mc.tasks._getScopeContexts()[cKey]
    if contexts <> invalid
      key = mc.tasks._getContextKey(node, fieldName, callback, targetField, scope)
      contexts.delete(key)
      if contexts.count() = 0
        mc.tasks.cleanNodeObserver(node, fieldName)
      else
      end if
    end if

    return true
  end function

  function cleanNodeObserver(node, fieldName)
    if node <> invalid and fieldName <> invalid and m._nodeContexts <> invalid
      m._nodeContexts.delete(node._oid + "_" + fieldName)
      node.unobserveFieldScoped(fieldName)
    end if
  end function

  function cleanScope()
    contexts = mc.tasks._getScopeContexts()
    for each contextKey in contexts
      context = contexts[contextKey]
      for each key in context
        c = context[key]
        mc.tasks.cleanNodeObserver(c.node, c.fieldName)
      end for
    end for
    m._nodeContexts = {}
  end function

  function cancelTask(task)
    if task <> invalid
      mc.tasks.cleanNodeObserver(task, task._observedField)
      task.control = "stop"
    end if
  end function

  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
  '++ ValueObserver class
  '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  @strict
  class ValueObserver
    private target
    private pendingComparators = {}
    private comparators = {}

    function new(node, field, target)
      m.target = target
      mc.tasks.observeNodeField(node, field, m.valueCallback, "both", false, m)
    end function

    function reset(hardReset = false)
      m.pendingComparators = {}
      if hardReset
        m.comparators = {}
      else
        m.pendingComparators.append(m.comparators)
      end if
    end function

    function destroy()
      m.reset(true)
      for each key in m.comparators
        comparator = m.comparators[key]
        mc.tasks.unobserveNodeField(node, field, comparator.cb, m.target)
      end for
    end function

    function onValue(value, func, comparator = "=", mode = "value")
      m.comparators[`${value}_${comparator}`] = { v: value, cb: func, c: comparator, mode: mode }
    end function

    function valueCallback(value, node)
      if m.pendingComparators.count() > 0
        for each id in m.pendingComparators
          o = m.pendingComparators[id]
          if (o.c = "=" and value = o.v) or (o.c = ">" and value > o.v) or (o.c = ">=" and value >= o.v) or (o.c = "<" and value < o.v) or (o.c = "<=" and value <= o.v) or (o.c = "<>" and value <> o.v)
            m.pendingComparators.delete(id)
            if mc.isAACompatible(m.target)
              m.target.__cb = o.cb
              if o.mode = "both"
                'bs:disable-next-line
                m.target.__cb(value, node)
              else if o.mode = "value"
                'bs:disable-next-line
                m.target.__cb(value)
              else
                'bs:disable-next-line
                m.target.__cb()
              end if
              m.target.delete("__cb")
            else
              executeInScope = sub(this, callback, mode)
                m = this
                if mode = "both"
                  'bs:disable-next-line
                  callback(value, node)
                else if mode = "value"
                  'bs:disable-next-line
                  callback(value)
                else
                  'bs:disable-next-line
                  callback()
                end if
              end sub
              executeInScope(target.scope, o.cb, o.mode)
            end if
          end if
        end for
      end if
    end function
  end class
end namespace