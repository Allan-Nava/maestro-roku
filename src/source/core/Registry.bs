import "pkg:/source/roku_modules/log/LogMixin.brs"

namespace mc
  class Registry

    private log = new log.Logger("Registry")
    '
    ' Read and deserialize to a native type
    public function read(sectionName as string, key as string) as dynamic
      sectionName = lcase(sectionName)
      key = lcase(key)
      section = createObject("roRegistrySection", sectionName)

      if section.exists(key)
        return parseJson(section.read(key))
      else
        return invalid
      end if
    end function

    public function write(sectionName as string, key as string, value as dynamic) as void
      sectionName = lcase(sectionName)
      key = lcase(key)
      registry = createObject("roRegistry")
      section = createObject("roRegistrySection", sectionName)
      section.write(key, formatJson(value))
      section.flush()
      registry.flush()
    end function


    public function delete(sectionName as string, key as string) as void
      sectionName = lcase(sectionName)
      key = lcase(key)
      section = createObject("roRegistrySection", sectionName)
      if section.exists(key)
        section.delete(key)
      end if
    end function

    public function readAll() as object
      registry = createObject("roRegistry")
      sections = registry.getSectionList()
      data = {}

      for each sectionName in sections
        section = createObject("roRegistrySection", sectionName)
        keys = section.getKeyList()
        sectionData = {}

        for each k in keys
          sectionData[k] = parseJson(section.read(k))
        end for
        data[sectionName] = sectionData
      end for

      return data
    end function

    public function writeAll(data as object) as void
      registry = createObject("roRegistry")

      if data <> invalid and type(data) = "roAssociativeArray"

        for each sectionName in data
          sectionData = data[sectionName]
          sectionName = lcase(`${sectionName}`)
          section = createObject("roRegistrySection", sectionName)

          if sectionData <> invalid and type(sectionData) = "roAssociativeArray"

            for each key in sectionData
              value = sectionData[key]
              key = lcase(`${key}`)
              section.write(key, formatJson(value))
            end for

          end if
          section.flush()

        end for

      end if

      registry.flush()
    end function


    '
    public function deleteAll() as void
      registry = createObject("roRegistry")
      sections = registry.getSectionList()

      for each sectionName in sections
        registry.delete(sectionName)
      end for
    end function


    function writeSerializable(section as string, key as string, serializable as object) as boolean
      if m.isSerializable(serializable)
        if type(serializable) = "roSGNode"
          data = serializable@.serialize()
        else
          'bs:disable-next-line
          data = serializable.serialize()
        end if

        m.write(section, key, data)
        return true
      else
        m.log.warn("asked to write from non-serializebale object")
      end if
      return false
    end function

    function readSerializable(section as string, key as string, serializable as object) as boolean
      if m.isSerializable(serializable)
        data = m.read(section, key)
        if type(serializable) = "roSGNode"
          serializable@.deserialize(data)
        else
          'bs:disable-next-line
          serializable.deserialize(data)
        end if
        return true
      else
        m.log.warn("asked to read into non-serializebale object")
      end if
      return false
    end function

    private function isSerializable(item as object) as boolean
      if mc.isAACompatible(item)
        if type(item) = "roSGNode"
          'we can't tell if a node has a certain function, so we have to guess
          return true
        else
          return item.serialize <> invalid and item.deserialize <> invalid
        end if
      end if
      return false
    end function

  end class
end namespace