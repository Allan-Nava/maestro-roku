
namespace mc.collections
  ' /**
  '  * @module Map
  '  * @description simple map to prevent excess copies when accessing sets of data
  '  */
  @node("mc_Map", "Node")
  class Map

    protected data = {}

    @observer("onDebugChange")
    public _debug = false

    public __contents as mc.types.AssocArray

    public length = 0

    public function clear()
      m.data = {}
      m.top.length = 0
    end function

    public function remove(key)
      m.data.delete(key)
      m.top.length = m.data.count()
    end function

    public function set(key, value)
      m.data[key] = value
      m.top.length = m.data.count()
    end function

    public function append(other)
      m.data.append(other)
      m.top.length = m.data.count()
    end function

    public function get(key)
      return m.data[key]
    end function

    public function hasKey(key)
      return m.data[key] <> invalid
    end function

    public function getValues()
      return m.data
    end function

    private function onDebugChange(value)
      ? "dumping contents"
      m.top.__contents = m.data
    end function
  end class
end namespace