import "pkg:/source/roku_modules/log/LogMixin.brs"
import "pkg:/source/core/Utils.bs"
import "pkg:/source/view/NodeClass.bs"

namespace mv
  @node("mv_StyleManager", "Node")
  class StyleManager extends mv.NodeClass

    private styles = {}
    private cachedStyles = {}

    function new()
      super("StyleManager")
      m.log = new log.Logger("StyleManager")
      m.setStyleJson(invalid)
    end function

    public function setStyleJson(json as mc.types.assocarray)
      if mc.isAACompatible(json)
        m.styles = json
      else
        m.styles = {}
      end if
      m.cachedStyles = {} ' we lazily pull out styles and parse them on demand
    end function

    ' /**
    '  * @member getStyle
    '  * @memberof module:StyleManager
    '  * @instance
    '  * @function
    '  * @description gets a maestro compliant style
    '  * @param {string} key
    '  * @returns {assocarray} style, marshalled for maestro compatible values, if a key is an assocarray with
    '  * @returns {boolean} isNested,internal key that indicates if this style is nested or not
    '  * @returns {boolean} style, marshalled for maestro compatible values, if a key is an assocarray with
    '  */
    public function getStyle(key as string, isNested = false as boolean) as dynamic
      if key = invalid or key.trim() = ""
        return {}
      end if

      style = m.cachedStyles[key]

      if style = invalid
        style = m.parseStyle(key)
        if style <> invalid
          m.cachedStyles[key] = style
        else if isNested
          return invalid
        else
          style = {}
        end if
      end if

      return style
    end function

    private function parseStyle(key as string)
      style = mc.getPath(m.styles, key)

      if style <> invalid
        m.resolveNestedStyles(style)
      end if
      return style
    end function

    private function resolveNestedStyles(style as string)
      if mc.isAACompatible(style)
        for each key in style
          style[key] = m.resolveNestedStyleValue(style[key])
        end for
      else if mc.isArray(style)
        n = style.count()
        for i = 0 to n
          style[i] = m.resolveNestedStyleValue(style[i])
        end for
      end if
    end function

    private function resolveNestedStyleValue(value as dynamic) as dynamic
      if mc.isString(value) and len(value) > 1 and left(value, 1) = "~"
        nestedKey = right(value, len(value) - 1)
        nestedStyle = m.getStyle(nestedKey, true)
        if nestedStyle = invalid
          m.log.warn("Could not find nested style with key", nestedKey)
          'leave the value as is - it _might_ not have been a nested style
          nestedStyle = value
        end if
        return nestedStyle
      else if mc.isArray(value) or mc.isAACompatible(value)
        return m.resolveNestedStyles(value)
      else
        return value
      end if
    end function
  end class
end namespace